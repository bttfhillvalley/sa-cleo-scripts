:CarClock
{$INCLUDE variables.txt}
if
    Car.wrecked(vehicle)
then
    return
end
// Hours
for var1 = 10 to 21 step 1
    3F11: set_car vehicle component "consoleclockdigithour" index var1 visible 0
end
00BF: var1 = current_time_hours, var3 = current_time_minutes
if
    var1 > 12
then
    var1 -= 12
end
var2 = var1
var2 /= 10
var2 *= 10
var1 -= var2
var2 /= 10
3F11: set_car vehicle component "consoleclockdigithour2" index var2 visible 1
3F11: set_car vehicle component "consoleclockdigithour1" index var1 visible 1

// Minutes
for var1 = 10 to 25
	3F11: set_car vehicle component "consoleclockdigitmin" index var1 visible 0
end
var1 = var3
var2 = var3
var2 /= 10
var2 *= 10
var1 -= var2
var2 /= 10
3F11: set_car vehicle component "consoleclockdigitmin2" index var2 visible 1
3F11: set_car vehicle component "consoleclockdigitmin1" index var1 visible 1

// Bulova
00BF: var1 = current_time_hours, var2 = current_time_minutes
008D: 1@ = integer_to_float var1
008D: 2@ = integer_to_float var2
008D: 3@ = integer_to_float var2
1@ *= 30.0
2@ *= 6.0
3@ *= 0.5
005B: 1@ += 3@
3F16: set_car vehicle component "HourHand" angle 0.0 1@ 0.0
3F16: set_car vehicle component "MinuteHand" angle 0.0 2@ 0.0

// Colon
3F10: set_car vehicle component "consoleclockdigitcolon" visible 0
if
    timera < 500
then
    3F10: set_car vehicle component "consoleclockdigitcolon" visible 1
end
if
    timera > 1000
then
    timera = 0
end
return