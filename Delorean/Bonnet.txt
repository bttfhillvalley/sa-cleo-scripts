{$CLEO .s}
{$INCLUDE ../Common/CleoConstants.txt}
0000:
int vehicle
float x
float y
float z
float vehicle_speed
float angle
float bonnet_speed
int iangle

angle = 0.0
while true
    wait 0
    if
        Car.Wrecked(vehicle)
    then
        terminate_this_custom_script
    end

    // If the bonnet is open and the car is moving, close the bonnet
    02E3: vehicle_speed = car vehicle speed
    if and
        angle > 0.0
        vehicle_speed > 30.0
    then
        gosub @CloseDoor
        continue
    end

    // Check to see if player is driving or not
    if
        00E0:   player $PLAYER_CHAR in_any_car
    then
        continue
    end
    0407: create_coordinate x y z from_car vehicle offset 0.0 3.25 0.0
    if or
        80E1:   not key_pressed 0 4
        80F6:   not player $PLAYER_CHAR 0 x y z  radius 0.75 0.75 2.0
    then
        continue
    end
    if
        angle <= 0.0
    then
        gosub @OpenDoor
    else
        gosub @CloseDoor
    end
end

:OpenDoor
bonnet_speed = 0.1
while angle < 1.0
    wait 0
    bonnet_speed *= 0.915
    angle += bonnet_speed
    3F1A: set_car vehicle bonnet angle
end
angle = 1.0 // Needed because of floating point inprecision
3F1A: set_car vehicle bonnet angle
return

:CloseDoor
while angle > 0.0
    wait 0
    angle -= 0.1
    3F1A: set_car vehicle bonnet angle
end
angle = 0.0  // Needed because of floating point inprecision
3F1A: set_car vehicle bonnet angle
return